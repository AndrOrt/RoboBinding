buildscript {
	repositories {
		mavenCentral()
	}
}

archivesBaseName = 'extras'

repositories {
    mavenCentral()
}

dependencies {
	compile(project(':framework'))
	compile fileTree(dir: 'libs', include: '*.jar')
	
	testCompile project(':framework').sourceSets.test.output
	//testCompile files(project(':framework').sourceSets.test.output.classesDir)
	//testCompile project(path: ':framework', configuration: 'testOutput')
	testCompile ("org.robolectric:robolectric:$robolectricVersion") {
		exclude group: 'com.google.android.maps', module: 'maps'
		exclude group: 'com.android.support', module: 'support-v4'
		exclude module: 'classworlds'
		exclude module: 'maven-artifact'
		exclude module: 'maven-artifact-manager'
		exclude module: 'maven-error-diagnostics'
		exclude module: 'maven-model'
		exclude module: 'maven-plugin-registry'
		exclude module: 'maven-profile'
		exclude module: 'maven-project'
		exclude module: 'maven-settings'
		exclude module: 'nekohtml'
		exclude module: 'plexus-container-default'
		exclude module: 'plexus-interpolation'
		exclude module: 'plexus-utils'
		exclude module: 'wagon-file'
		exclude module: 'wagon-http-lightweight'
		exclude module: 'wagon-http-shared'
		exclude module: 'wagon-provider-api'
	}
	testCompile "org.robolectric:shadows-support-v4:$robolectricVersion"
	testCompile "com.google.guava:guava:$guavaVersion"
}

javadoc {
	source = sourceSets.main.allJava
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

apply plugin: 'signing'
signing {
	required { !version.endsWith('SNAPSHOT') && gradle.taskGraph.hasTask("uploadArchives") }
	sign configurations.archives
}

apply plugin: 'maven'
uploadArchives {
	repositories {
		mavenDeployer {
			beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
			
			repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
				authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
			}
			
			snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
				authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
			}
			
			//repository(url: "file://$projectDir/mavenRepo")
			pom.project {
				name 'RoboBinding Code Generation'
				description 'A set of RoboBinding Code Generation tools by utilizing Java annotation processors'
				url 'http://robobinding.org'
				
				scm {
					url 'https://github.com/RoboBinding/RoboBinding'
					connection 'scm:git:git@github.com:RoboBinding/RoboBinding.git'
					developerConnection 'scm:git:git@github.com:RoboBinding/RoboBinding.git'
				}
				
				ciManagement {
					system 'Cloudbees Jenkins'
					url 'https://robobindingacc.ci.cloudbees.com/'
				}

				licenses {
					license {
						name 'Apache License Version 2.0'
						url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
						distribution 'repo'
						comments """
						Copyright 2012-2014 Cheng Wei and RoboBinding Contributors
						
						Licensed under the Apache License, Version 2.0 (the "License");
						you may not use this file except in compliance with the License.
						You may obtain a copy of the License at
						
						  http://www.apache.org/licenses/LICENSE-2.0
						
						Unless required by applicable law or agreed to in writing, software
						distributed under the License is distributed on an "AS IS" BASIS,
						WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
						See the License for the specific language governing permissions
						and limitations under the License.
						"""
					}
				}

				developers {
					developer {
						name 'Cheng Wei'
						email 'weicheng112@gmail.com'
						organization = 'RoboBinding Open Source'
						organizationUrl 'http://robobinding.org'
						roles {
							role 'Developer'
						}
					}
					developer {
						name 'RoboBinding Contributors'
						roles {
							role 'Developer'
						}
					}
				}
			}
			
			pom.withXml {
				def dependenciesNode = it.asNode().dependencies[0];
				//remove android sdk dependency node.
				def robobindingDependencyNode = dependenciesNode.dependency.find{ it.artifactId.text() == 'robobinding' }
				dependenciesNode.remove robobindingDependencyNode
			}
		}
	}
}

apply plugin: 'eclipse'

eclipse {
	project {
		name = 'RoboBinding-extras'
		comment = 'The RoboBinding extra library for android support library and other third libraries'
		
		natures 'org.springsource.ide.eclipse.gradle.core.nature'
	}
	
	classpath {
		containers 'org.springsource.ide.eclipse.gradle.classpathcontainer'
		file {
			whenMerged { classpath ->
				classpath.entries.removeAll { entry ->
					entry.kind == 'lib'
				}
			}
		}

		file.withXml {
			def node = it.asNode()
			
			//node.remove( node.find { it.@path == 'src/test/resources' } )
			//node.appendNode('classpathentry excluding="**" kind="src" path="src/test/resources"')
		}
	}
}
  
